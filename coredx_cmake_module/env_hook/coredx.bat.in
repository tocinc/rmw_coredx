set "CoreDX_HOME=@CoreDX_HOME@"

:: Call CoreDX's env setup script, piping stdout to nul, since they have echo on.
:: Note: if more than one CoreDX DDS target or host platform is installed, the script
:: will request user input to select the host or platform or both.
:: call "%CoreDX_HOME:/=\%\scripts\cdxenv.bat" 1> nul
:: Add the CoreDX_LIBRARY_DIR to the Path so .dll's can be found at runtime.
set "CoreDX_LIBRARY_DIR=%COREDX_TOP%\@CoreDX_LIBRARY_RELATIVE_DIR@"
call:ament_prepend_unique_value PATH "%CoreDX_LIBRARY_DIR:/=\%"
call:ament_prepend_unique_value PATH "%COREDX_TOP:/=\%\host\%COREDX_HOST%\bin"
set "CoreDX_LIBRARY_DIR="

goto:eof

:: function to prepend non-duplicate values to environment variables
:: using colons as separators and avoiding trailing separators
:ament_prepend_unique_value
  setlocal enabledelayedexpansion
  :: arguments
  set "_listname=%~1"
  set "_value=%~2"
  :: expand the list variable
  set "_list=!%_listname%!"
  :: check if the list contains the value
  set "_is_duplicate="
  if "%_list%" NEQ "" (
    for %%a in ("%_list:;=";"%") do (
      if "%%~a" == "%_value%" set "_is_duplicate=1"
    )
  )
  :: if it is not a duplicate prepend it
  if "%_is_duplicate%" == "" (
    :: produces a trailing semicolon when the list empty, but that's ok
    set "_list=%_value%;%_list%"
  )
  (endlocal
    set "%~1=%_list%"
  )
goto:eof
